.include "cpu.i65"
.include "syscall.i65"
.include "syszp.i65"
.include "taskzp.i65"
.include "version.i65"
.p816

.segment "SYSINIT"

	jmp reset
.export version
version:
	.byte _minor_version
	.byte _major_version
	.byte "X65OS ROM BIOS "
	.byte _git_version, 13, 10
	.byte "Copyright (c) 2020 Calle Englund", 13, 10, 0

reset:
	sei
	; Native mode
	clc
	xce
	; 16-bit registers
	rep #CPU_M|CPU_X
	.a16
	.i16
	; Reset direct page to page 0
	lda #$0000
	tcd
	; Reset stack pointer to page 0
	lda #$00ff
	tcs

; Clear syszp and systk pages
.scope clearlowpages
	rep #CPU_M|CPU_X
	.a16
	.i16
	ldx #$01fe
loop:
	stz $00,x
	dex
	dex
	bpl loop
.endscope

.scope idle

.segment "CODE"

	pea task
	sys async, 1
	; Should never return, unless something went really wrong
	brk

; The IDLE task
.segment "RODATA"

task:
	.asciiz "IDLE TASK  "	; Task name
	.byte 0					; # of stack pages
	.byte $00				; starting data bank
	.word $0000				; starting Y
	.word $0000				; starting X
	.word $0000				; starting C
	.byte %00000000			; starting P
	.faraddr start			; starting PB:PC

.segment "CODE"

start:
	.a16
	.i16
@again:
	; This might be a good time to wake up pending tasks on the wait queue
	; and record runtime statistics

	; BEGIN CRITICAL SECTION
	; Can't allow any IRQ to run until we WAIt
	sei
		lda taskzp::next
		bne @notalone
		; next,prev both zero => only task currently running, go to sleep
		wai
@notalone:
	cli
	; END CRITICAL SECTION

	; No need to yield if we're alone
	beq @again
	sys yield
	bra @again

.endscope

; IRQ handler prologue, must match .struct irq_sf in _irq.i65
.segment "IRQCODE"
	rep #CPU_M|CPU_X
	.a16
	.i16
	pha
	phx
	phy
	; Switch to system direct page, saving the current task on stack
	phd
	lda #$0000
	tcd
	; IRQ event flags
	pha
	; IRQ handlers expect A8, I16
	sep #CPU_M
	.a8
